buildscript {
  ext {
    springBootVersion = '2.1.5.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3')
    classpath('gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10')
    classpath('net.ltgt.gradle:gradle-apt-plugin:0.21')
    classpath('com.google.guava:guava:18.0')
  }

}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'
apply plugin: 'com.ewerk.gradle.plugins.querydsl'
apply plugin: "net.ltgt.apt"
apply plugin: 'net.ltgt.apt-idea'
apply plugin: "groovy"

group = 'life.coachy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-data-mongodb')
  compile('org.springframework.boot:spring-boot-starter-security')
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }

  compile('org.springframework.boot:spring-boot-starter')
  compile('io.springfox:springfox-swagger2:2.9.2')
  compile('io.springfox:springfox-swagger-ui:2.9.2')
  compile('org.hibernate.validator:hibernate-validator:6.0.16.Final')
  compile('org.hibernate.validator:hibernate-validator-annotation-processor:6.0.16.Final')
  compile('commons-beanutils:commons-beanutils:1.9.3')
  compile('org.springframework.boot:spring-boot-starter-mail:2.1.5.RELEASE')
  compile('org.springframework.boot:spring-boot-starter-thymeleaf')
  compile('org.springframework.boot:spring-boot-starter-aop')
  compile('org.springframework.boot:spring-boot-starter-websocket')
  compile('org.springframework.boot:spring-boot-starter-amqp')
  compile('org.springframework.shell:spring-shell-starter:2.0.0.RELEASE')

  compile('io.projectreactor.netty:reactor-netty')
  compile('io.netty:netty-all')

  annotationProcessor('com.querydsl:querydsl-apt:4.1.4')
  compile('com.querydsl:querydsl-mongodb:4.1.4')
  compile(group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.7', ext: 'pom')

  testCompile('com.icegreen:greenmail:1.5.5')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.security:spring-security-test')
  testCompile('de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0')
  testCompile("org.spockframework:spock-core:1.3-groovy-2.5")
  testCompile("org.spockframework:spock-spring:1.3-groovy-2.5")
}

querydsl {
  querydslSourcesDir = 'build/generated/sources/annotationProcessor/java/main/'
  springDataMongo = true
}

task ciTest(type: ContinuousIntegrationTest) {}

test {
  workingDir = new File("$buildDir/resources")
}

class ContinuousIntegrationTest extends Test {
  ContinuousIntegrationTest() {
    systemProperty 'spring.profiles.active', 'ci'
  }
}
